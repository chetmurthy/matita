(**************************************************************************)
(*       ___                                                              *)
(*      ||M||                                                             *)
(*      ||A||       A project by Andrea Asperti                           *)
(*      ||T||                                                             *)
(*      ||I||       Developers:                                           *)
(*      ||T||         The HELM team.                                      *)
(*      ||A||         http://helm.cs.unibo.it                             *)
(*      \   /                                                             *)
(*       \ /        This file is distributed under the terms of the       *)
(*        v         GNU General Public License Version 2                  *)
(*                                                                        *)
(**************************************************************************)

include "basic_2/notation/relations/rajuststar_5.ma".
include "basic_2/substitution/fpa.ma".

(* MULTIPLE AJUSTMENT *******************************************************)

definition fpas: bool → bi_relation lenv term ≝ λs. bi_TC … (fpa s).

interpretation
   "multiple ajustment (restricted closure)"
   'RAjustStar L1 T1 s L2 T2 = (fpas s L1 T1 L2 T2).

(* Basic eliminators ********************************************************)

lemma fpas_ind: ∀L1,T1,s. ∀R:relation2 …. R L1 T1 →
                (∀L,L2,T,T2. ⦃L1, T1⦄ ⇳*[s] ⦃L, T⦄ → ⦃L, T⦄ ⇳[s] ⦃L2, T2⦄ → R L T → R L2 T2) →
                ∀L2,T2. ⦃L1, T1⦄ ⇳*[s] ⦃L2, T2⦄ → R L2 T2.
#L1 #T1 #s #R #IH1 #IH2 #L2 #T2 #H
@(bi_TC_star_ind … IH1 IH2 L2 T2 H) //
qed-.

lemma fpas_ind_dx: ∀L2,T2,s. ∀R:relation2 …. R L2 T2 →
                   (∀L1,L,T1,T. ⦃L1, T1⦄ ⇳[s] ⦃L, T⦄ → ⦃L, T⦄ ⇳*[s] ⦃L2, T2⦄ → R L T → R L1 T1) →
                   ∀L1,T1. ⦃L1, T1⦄ ⇳*[s] ⦃L2, T2⦄ → R L1 T1.
#L2 #T2 #s #R #IH1 #IH2 #L1 #T1 #H
@(bi_TC_star_ind_dx … IH1 IH2 L1 T1 H) //
qed-.

(* Basic properties *********************************************************)

lemma fpas_refl: ∀s. bi_reflexive … (fpas s).
/2 width=1 by bi_inj/ qed.

lemma fpas_sym: ∀s. bi_symmetric … (fpas s).
/3 width=1 by fpa_sym, bi_TC_symmetric/ qed-.

lemma fpa_fpas: ∀L1,L2,T1,T2,s. ⦃L1, T1⦄ ⇳[s] ⦃L2, T2⦄ → ⦃L1, T1⦄ ⇳*[s] ⦃L2, T2⦄.
/2 width=1 by bi_inj/ qed.

lemma fpas_strap1: ∀L1,L,L2,T1,T,T2,s. ⦃L1, T1⦄ ⇳*[s] ⦃L, T⦄ → ⦃L, T⦄ ⇳[s] ⦃L2, T2⦄ →
                   ⦃L1, T1⦄ ⇳*[s] ⦃L2, T2⦄.
/2 width=4 by bi_step/ qed-.

lemma fpas_strap2: ∀L1,L,L2,T1,T,T2,s. ⦃L1, T1⦄ ⇳[s] ⦃L, T⦄ → ⦃L, T⦄ ⇳*[s] ⦃L2, T2⦄ →
                   ⦃L1, T1⦄ ⇳*[s] ⦃L2, T2⦄.
/2 width=4 by bi_TC_strap/ qed-.
